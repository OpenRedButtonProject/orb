# Useful references: https://gn.googlesource.com/gn/+/main/docs/reference.md

# This is done by the calling script already
# import("//build/config/BUILDCONFIG.gn")

# Define the actual library target
static_library("orb") {
  sources = [
    "moderator/Moderator.cpp",
    "moderator/AppMgrInterface.cpp",
    "moderator/BroadcastInterface.cpp",
    "moderator/Network.cpp",
    "moderator/MediaSynchroniser.cpp",
    "moderator/app_mgr/ait.cpp",
    "moderator/app_mgr/application_manager.cpp",
    "moderator/app_mgr/hbbtv_app.cpp",
    "moderator/app_mgr/opapp.cpp",
    "moderator/app_mgr/utils.cpp",
    "moderator/app_mgr/xml_parser.cpp",
    "moderator/Drm.cpp",
    "moderator/JsonRpcCallback.cpp",
  ]

  deps = [
    "//base", # For logging dependency
    "//third_party/jsoncpp",
    "//third_party/libxml",
    ":orb_util",
    ":orb_network_services",
  ]

  include_dirs = [
   "moderator",
   "moderator/utilities"
  ]

  public_deps = []

  public_configs = [ ":external_lib_config" ]
}

# Expose the include directory
config("external_lib_config") {
  include_dirs = [
   "include",
   ]

  defines = [ "IS_CHROMIUM", "ORB_HBBTV_VERSION=204"]
}

static_library("opapp_package_manager") {
  sources = [
    "package_manager/OpAppPackageManager.cpp",
    "package_manager/HashCalculator.cpp",
    "package_manager/HashCalculator.h",
    "package_manager/Decryptor.cpp",
    "package_manager/Decryptor.h",
  ]

  deps = [
    "//base", # For logging dependency
    "//third_party/jsoncpp",
    "//third_party/boringssl:boringssl"
  ]

  public_deps = []

  public_configs = [ ":external_lib_config" ]
}

static_library("orb_integration")
{
  testonly = true

  sources = [
    "integration/OrbIntegration.cpp"
  ]

  public_configs = [ ":external_lib_config" ]

  deps = [
    "//content/public/browser:browser",
    "//content/shell:content_shell_lib"
  ]
}

static_library("orb_util")
{
  sources = [
    "moderator/utilities/JsonUtil.cpp",
    "moderator/utilities/StringUtil.cpp",
  ]

  public_configs = [ ":util_lib_config" ]

  include_dirs = [
   "include",
   "moderator",
  ]

  deps = [
    "//third_party/jsoncpp",
    "//base", # For logging dependency
  ]

  defines = [ "IS_CHROMIUM" ]
}

config("util_lib_config") {
  include_dirs = [
   "moderator/utilities",
   ]
}


executable("test_orb_moderator")
{
  sources = [ "test/moderator_unittest.cpp"]

  deps = [
    "//testing/gtest",
    "//testing/gtest:gtest_main",
    ":orb"
  ]

  testonly = true
}

executable("test_opapp_package_manager")
{
  sources = [
    "test/opapp_package_manager_unittest.cpp",
    "test/OpAppPackageManagerTestInterface.cpp",
    "test/OpAppPackageManagerTestInterface.h"
  ]

  deps = [
    "//testing/gtest",
    "//testing/gtest:gtest_main",
    ":opapp_package_manager"
  ]

  testonly = true
}

executable("test_orb_util")
{
  sources = [
    "test/json_util_unittest.cpp",
    "test/string_util_unittest.cpp",
  ]

  deps = [
    "//testing/gtest",
    "//testing/gtest:gtest_main",
    "//third_party/jsoncpp",
    "//base", # For logging dependency
    ":orb_util",
  ]

  include_dirs = [
    "include",
    "moderator/utilities",
  ]

  testonly = true
}

static_library("orb_network_services") {
  sources = [
    "moderator/network_services/json_rpc/JsonRpcService.cpp",
    "moderator/network_services/json_rpc/JsonRpcServiceUtil.cpp",
    "moderator/network_services/websocket_service.cpp"
  ]

  deps = [
    "//base", # For logging dependency
    "//third_party/jsoncpp",
    "//third_party/orb/external/libwebsockets/v4.3:libwebsockets",
    ":orb_util",
  ]

  public_deps = []

  public_configs = [
    ":external_network_services_config",
    "//third_party/orb/external/libwebsockets/v4.3:libwebsockets_public_config",
    "//third_party/jsoncpp:jsoncpp_config"
   ]
}

# Expose the include directory
config("external_network_services_config") {
  include_dirs = [
    "moderator/network_services/include",
    "moderator/network_services/json_rpc",
  ]
  defines = [ "IS_CHROMIUM" ]
}

executable("test_orb_jsonrpcservice")
{
  sources = [
    "moderator/network_services/test/jsonrpcservice_unittest.cpp",
    "moderator/network_services/test/jsonrpcserviceutil_unittest.cpp"
  ]

  deps = [
    "//testing/gtest",
    "//testing/gtest:gtest_main",
    "//base", # For logging dependency,
    ":orb_network_services"
  ]
  testonly = true
}

